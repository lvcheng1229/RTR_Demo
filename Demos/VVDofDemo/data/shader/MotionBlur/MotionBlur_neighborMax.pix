#version 120 or 150 compatibility// -*- c++ -*-
#extension GL_EXT_gpu_shader4 : require
#include <compatibility.glsl>

/** \file MotionBlur_neighborMax.pix 

    Computes the largest-magnitude velocity of any point that could
    possibly overlap points in the tile with corner at gl_FragCoord.xy * maxBlurRadius.

    Invoked from MotionBlur::computeNeighborMax().
*/

uniform float2      SS_POSITION_CHANGE_readScaleBias;
uniform float2      SS_POSITION_CHANGE_writeScaleBias;

// This is actually the tileMax buffer, but the way that the GBuffer
// infrastructure works renames it to the SS_POSITION_CHANGE_buffer
// from which it was computed.
uniform sampler2D   SS_POSITION_CHANGE_buffer;

#define tileMax SS_POSITION_CHANGE_buffer

#if __VERSION__ < 130
#   define neighborMax gl_FragColor.rg
#else
    out float2 neighborMax;
#endif

// Only gather neighborhood velocity into tiles that could be affected by it.
// In the general case, only six of the eight neighbors contribute:
//
//  This tile can't possibly be affected by the center one
//     |
//     v
//    ____ ____ ____
//   |    | ///|/// |
//   |____|////|//__|    
//   |    |////|/   |
//   |___/|////|____|    
//   |  //|////|    | <--- This tile can't possibly be affected by the center one
//   |_///|///_|____|    
//
void main() {
    // Vector with the largest magnitude
    float2 m = float2(0.0);
    
    // Squared magnitude of m
    float largestMagnitude2 = -1.0;

    int2 maxCoord = textureSize(tileMax, 0) - int2(1);
    
    int2 currentTile = int2(gl_FragCoord.xy);
    int2 offset;
    for (offset.y = -1; offset.y <= +1; ++offset.y) {
        for (offset.x = -1; offset.x <= +1; ++offset.x) {
            
            int2 neighborTile = currentTile + offset;
            float2 vmax_neighbor = texelFetch(tileMax, clamp(neighborTile, int2(0), maxCoord), 0).rg * SS_POSITION_CHANGE_readScaleBias.x + SS_POSITION_CHANGE_readScaleBias.y;

            // Magnitude squared
            float magnitude2_neighbor = dot(vmax_neighbor, vmax_neighbor);

            if (magnitude2_neighbor > largestMagnitude2) {
                
                // Non-unit
                float2 directionOfVelocity = vmax_neighbor;
                
                // Manhattan distance to the tiles, which is used for
                // differentiating corners versus middle blocks
                float displacement = abs(offset.x) + abs(offset.y);

                // Relative sign on each axis of the offset compared
                // to the velocity for that tile.  In order for a tile
                // to affect the center tile, it must have a
                // neighborhood velocity in which x and y both have
                // identical or both have opposite signs relative to
                // offset. If the offset coordinate is zero then
                // velocity is irrelevant.
                float2 point = sign(offset * directionOfVelocity);

                float distance = (point.x + point.y);
				
                // Here's an example of the logic for this code.
                // In this diagram, the upper-left tile has offset = (-1, -1).
                // V1 is velocity = (1, -2). point in this case = (-1, 1), and therefore distance = 0, 
                // so the upper-left tile does not affect the center.
                //
                // Now, look at another case. V2 = (-1, -2). point = (1, 1), so distance = 2 and the tile does
                // affect the center.
                //
		// V2(-1,-2)  V1(1, -2) 
		//		  \	   /
		//         \  /
		//          \/___ ____ ____
		//	(-1, -1)|    |    |    |
		//			|____|____|____|    
		//			|    |    |    |
		//			|____|____|____|    
		//			|    |    |    | 
		//			|____|____|____|    
		//			
				
                if (abs(distance) == displacement) {
                    // This is the new largest PSF in the neighborhood
                    m = vmax_neighbor;
                    largestMagnitude2 = magnitude2_neighbor;
                } // larger
            } // larger
        } // x
    } // y

    neighborMax = m * SS_POSITION_CHANGE_writeScaleBias.x + SS_POSITION_CHANGE_writeScaleBias.y;
}
